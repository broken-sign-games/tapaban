title Tapaban
author Menderbug
homepage menderbug.itch.io
background_color #ddd
text_color #222

sprite_size 15x15

run_rules_on_level_start

again_interval 0.05

=====
TAGS
=====

U_ = up
L_ = left
R_ = right
D_ = down

Position = First Second Third Fourth
Value = _0 _1 _2 _3 _4 _5 _6 _7 _8
Validity = Valid Invalid
Frame = _8 _7 _6 _5 _4 _3 _2 _1 _0

Position1 = Position
Position2 = Position

Value1 = Value
Value2 = Value

Validity1 = Validity
Validity2 = Validity

Neighbor = _TL _T _TR _R _BR _B _BL _L
Neighbor1 = Neighbor
Neighbor2 = Neighbor
Neighbor3 = Neighbor
Neighbor4 = Neighbor
Neighbor5 = Neighbor
Neighbor6 = Neighbor
Neighbor7 = Neighbor
Neighbor8 = Neighbor

LessThan_1 = _0
LessThan_2 = _1 LessThan_1
LessThan_3 = _2 LessThan_2
LessThan_4 = _3 LessThan_3
LessThan_5 = _4 LessThan_4
LessThan_6 = _5 LessThan_5
LessThan_7 = _6 LessThan_6
LessThan_8 = _7 LessThan_7

LaterThan_Third = Fourth
LaterThan_Second = Third LaterThan_Third
LaterThan_First = Second LaterThan_Second

=========
MAPPINGS
=========

( Iterate through positions in reading order )
Position => NextPosition
First Second Third Fourth -> Third First Fourth Second

Frame => NextFrame
_8 _7 _6 _5 _4 _3 _2 _1 _0 -> _7 _6 _5 _4 _3 _2 _1 _0 _0

========
OBJECTS
========

Background 
#111 #222
111111111111111
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
111111111111111

CorrectBackground
#002200 #003300
111111111111111
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
111111111111111

Wall 
#ddd

Player 
#888
...............
.....00000.....
.....00000.....
......000......
......000......
.00...000...00.
.0000000000000.
.0000000000000.
.0000000000000.
.00...000...00.
......000......
......000......
.....00000.....
.....00000.....
...............


BlankCrate 
#ddd #bbb
111111111111111
110000010000011
101000010000101
100100010001001
100010010010001
100001010100001
100000111000001
111111111111111
100000111000001
100001010100001
100010010010001
100100010001001
101000010000101
110000010000011
111111111111111


ClueCrate 
#ddd #bbb
111111111111111
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
100000000000001
111111111111111

Clue:Position:Value:Validity
transparent

Clue:First:_1:Valid
#ddd #bbb #222
...............
...............
..02200........
..00200........
..00200........
..00200........
..02220........
...............
...............
...............
...............
...............
...............
...............
...............

Clue:First:_2:Valid
#ddd #bbb #222
...............
...............
..02200........
..00020........
..00200........
..02000........
..02220........
...............
...............
...............
...............
...............
...............
...............
...............

Clue:First:_3:Valid
#ddd #bbb #222
...............
...............
..02200........
..00020........
..00200........
..00020........
..02200........
...............
...............
...............
...............
...............
...............
...............
...............

Clue:First:_4:Valid
#ddd #bbb #222
...............
...............
..02000........
..02020........
..02220........
..00020........
..00020........
...............
...............
...............
...............
...............
...............
...............
...............

Clue:First:_5:Valid
#ddd #bbb #222
...............
...............
..02220........
..02000........
..02200........
..00020........
..02200........
...............
...............
...............
...............
...............
...............
...............
...............

Clue:First:_6:Valid
#ddd #bbb #222
...............
...............
..00220........
..02000........
..02200........
..02020........
..00200........
...............
...............
...............
...............
...............
...............
...............
...............

Clue:First:_7:Valid
#ddd #bbb #222
...............
...............
..02220........
..00020........
..00200........
..00200........
..00200........
...............
...............
...............
...............
...............
...............
...............
...............

Clue:First:_8:Valid
#ddd #bbb #222
...............
...............
..00200........
..02020........
..00200........
..02020........
..00200........
...............
...............
...............
...............
...............
...............
...............
...............

Clue:Third:Value:Valid
#ddd #bbb #222
copy: Clue:First:Value:Valid shift:right shift:right shift:right shift:right shift:right shift:right

Clue:Fourth:Value:Valid
#ddd #bbb #222
copy: Clue:First:Value:Valid shift:down shift:down shift:down shift:down shift:down shift:down

Clue:Second:Value:Valid
#ddd #bbb #222
copy: Clue:Third:Value:Valid shift:down shift:down shift:down shift:down shift:down shift:down

Clue:Position:Value:Invalid
#ddd #bbb red
copy: Clue:Position:Value:Valid

SingleClue:Value:Validity
transparent

SingleClue:_0:Valid
#ddd #bbb #222
...............
...............
..00000000000..
..00022222000..
..00222222200..
..00220002200..
..00220002200..
..00220002200..
..00220002200..
..00220002200..
..00222222200..
..00022222000..
..00000000000..
...............
...............

( Alternative sprite 

...............
...............
..00000000000..
..00000000000..
..00002220200..
..00020002000..
..00200020200..
..00200200200..
..00202000200..
..00020002000..
..00202220000..
..00000000000..
..00000000000..
...............
...............
)

SingleClue:_1:Valid
#ddd #bbb #222
...............
...............
..00000000000..
..00002220000..
..00222220000..
..00222220000..
..00002220000..
..00002220000..
..00002220000..
..00002220000..
..00222222200..
..00222222200..
..00000000000..
...............
...............

SingleClue:_2:Valid
#ddd #bbb #222
...............
...............
..00000000000..
..00022222000..
..00222222200..
..00220002200..
..00000022200..
..00000222000..
..00002220000..
..00022200000..
..00222222200..
..00222222200..
..00000000000..
...............
...............

SingleClue:_3:Valid
#ddd #bbb #222
...............
...............
..00000000000..
..00022222000..
..00222222200..
..00220002200..
..00000002200..
..00000222000..
..00000002200..
..00220002200..
..00222222200..
..00022222000..
..00000000000..
...............
...............

SingleClue:_4:Valid
#ddd #bbb #222
...............
...............
..00000000000..
..00002200000..
..00002200000..
..00022000000..
..00022022000..
..00220022000..
..00222222200..
..00222222200..
..00000022000..
..00000022000..
..00000000000..
...............
...............

SingleClue:_5:Valid
#ddd #bbb #222
...............
...............
..00000000000..
..00222222200..
..00222222200..
..00220000000..
..00222222000..
..00222222200..
..00000002200..
..00220002200..
..00222222200..
..00022222000..
..00000000000..
...............
...............

SingleClue:_6:Valid
#ddd #bbb #222
...............
...............
..00000000000..
..00022222200..
..00222222200..
..00220000000..
..00222222000..
..00222222200..
..00220002200..
..00220002200..
..00222222200..
..00022222000..
..00000000000..
...............
...............

SingleClue:_7:Valid
#ddd #bbb #222
...............
...............
..00000000000..
..00222222200..
..00222222200..
..00000002200..
..00000022000..
..00000220000..
..00000220000..
..00002200000..
..00002200000..
..00002200000..
..00000000000..
...............
...............

SingleClue:_8:Valid
#ddd #bbb #222
...............
...............
..00000000000..
..00022222000..
..00222222200..
..00220002200..
..00220002200..
..00022222000..
..00220002200..
..00220002200..
..00222222200..
..00022222000..
..00000000000..
...............
...............

SingleClue:Value:Invalid
#ddd #bbb red
copy: SingleClue:Value:Valid

Pipe:right Pipe:left
#ddd #bbb
000000000000000
000000000000000
000000000000000
000000000000000
100000000000001
110000000000011
111111111111111
111111111111111
111111111111111
110000000000011
100000000000001
000000000000000
000000000000000
000000000000000
000000000000000

Pipe:down Pipe:up
#ddd #bbb
copy: Pipe:right rot:right:down

ClueInPipe:_0:right:_4
#222 #bbb
...............
...............
...............
...............
......111......
.....11011.....
....1101011....
....1101011....
....1101011....
.....11011.....
......111......
...............
...............
...............
...............

ClueInPipe:_1:right:_4
#222 #bbb
...............
...............
...............
...............
......111......
.....10011.....
....1110111....
....1110111....
....1110111....
.....10001.....
......111......
...............
...............
...............
...............

ClueInPipe:_2:right:_4
#222 #bbb
...............
...............
...............
...............
......111......
.....10011.....
....1111011....
....1110111....
....1101111....
.....10001.....
......111......
...............
...............
...............
...............

ClueInPipe:_3:right:_4
#222 #bbb
...............
...............
...............
...............
......111......
.....10011.....
....1111011....
....1110111....
....1111011....
.....10011.....
......111......
...............
...............
...............
...............

ClueInPipe:_4:right:_4
#222 #bbb
...............
...............
...............
...............
......111......
.....10111.....
....1101011....
....1100011....
....1111011....
.....11101.....
......111......
...............
...............
...............
...............

ClueInPipe:_5:right:_4
#222 #bbb
...............
...............
...............
...............
......111......
.....10001.....
....1101111....
....1100111....
....1111011....
.....10011.....
......111......
...............
...............
...............
...............

ClueInPipe:_6:right:_4
#222 #bbb
...............
...............
...............
...............
......111......
.....11001.....
....1101111....
....1100111....
....1101011....
.....11011.....
......111......
...............
...............
...............
...............

ClueInPipe:_7:right:_4
#222 #bbb
...............
...............
...............
...............
......111......
.....10001.....
....1111011....
....1110111....
....1110111....
.....11011.....
......111......
...............
...............
...............
...............

ClueInPipe:_8:right:_4
#222 #bbb
...............
...............
...............
...............
......111......
.....11011.....
....1101011....
....1110111....
....1101011....
.....11011.....
......111......
...............
...............
...............
...............

ClueInPipe:Value:directions:_4
#222 #bbb
copy: ClueInPipe:Value:right:_4

ClueInPipe:Value:directions:_5
#222 #bbb
copy: ClueInPipe:Value:right:_4 shift:<

ClueInPipe:Value:directions:_6
#222 #bbb
copy: ClueInPipe:Value:right:_4 shift:< shift:<

ClueInPipe:Value:directions:_7
#222 #bbb
copy: ClueInPipe:Value:right:_4 shift:< shift:< shift:<

ClueInPipe:Value:directions:_8
#222 #bbb
copy: ClueInPipe:Value:right:_4 shift:< shift:< shift:< shift:<


ClueInPipe:Value:directions:_3
#222 #bbb
copy: ClueInPipe:Value:right:_4 shift:>

ClueInPipe:Value:directions:_2
#222 #bbb
copy: ClueInPipe:Value:right:_4 shift:> shift:>

ClueInPipe:Value:directions:_1
#222 #bbb
copy: ClueInPipe:Value:right:_4 shift:> shift:> shift:>

ClueInPipe:Value:directions:_0
#222 #bbb
copy: ClueInPipe:Value:right:_4 shift:> shift:> shift:> shift:>

Unreachable
darkred
...............
...............
...............
...............
...............
...............
......000......
......000......
......000......
...............
...............
...............
...............
...............
...............

2x2TopLeft
darkred
...............
...............
...............
...............
...............
...............
...............
...............
...............
...............
...............
...............
............00.
............00.
...............

2x2TopRight
darkred
copy: 2x2TopLeft rot:right:down

2x2BottomLeft
darkred
copy: 2x2TopLeft rot:down:right

2x2BottomRight
darkred
copy: 2x2TopLeft rot:down:up

UncluedBlock:Neighbor
transparent
(
UncluedBlock:_TL
darkred
...............
...............
...............
...............
...............
...............
...............
...............
...............
...............
...............
...............
............000
............000
............000

UncluedBlock:_TR
darkred
copy: UncluedBlock:_TL rot:right:down

UncluedBlock:_BL
darkred
copy: UncluedBlock:_TL rot:down:right

UncluedBlock:_BR
darkred
copy: UncluedBlock:_TL rot:down:up

UncluedBlock:_T
darkred
...............
...............
...............
...............
...............
...............
...............
...............
...............
...............
...............
...............
000000000000000
000000000000000
000000000000000

UncluedBlock:_R
darkred
copy: UncluedBlock:_T rot:right:down

UncluedBlock:_L
darkred
copy: UncluedBlock:_T rot:left:down

UncluedBlock:_B
darkred
copy: UncluedBlock:_T rot:down:up
)
_Temp
pink

_ProcessingClues
pink

_ProcessedClues
pink

_Processing:Neighbor
pink

_Above
pink

_Below
pink

_SplitterTarget:Position
pink

_2x2Candidate
pink

_StartList
pink

_EndList
pink

_Black
pink

_White
pink

_StartBlock
pink

_EndBlock
pink

_Source:Neighbor
pink

_StartCluedBlock
pink

_UnsortedClue:Position:Value
pink

_SortCursor:Position
pink

=======
LEGEND
=======

. = Background
# = Wall
═ = Pipe:right
║ = Pipe:down
P = Player
* = BlankCrate
0 = ClueCrate and SingleClue:_0:Valid
1 = ClueCrate and SingleClue:_1:Valid
2 = ClueCrate and SingleClue:_2:Valid
3 = ClueCrate and SingleClue:_3:Valid
4 = ClueCrate and SingleClue:_4:Valid
5 = ClueCrate and SingleClue:_5:Valid
6 = ClueCrate and SingleClue:_6:Valid
7 = ClueCrate and SingleClue:_7:Valid
8 = ClueCrate and SingleClue:_8:Valid

a = ClueCrate and Clue:First:_1:Valid and Clue:Second:_1:Valid
b = ClueCrate and Clue:First:_1:Valid and Clue:Second:_2:Valid
c = ClueCrate and Clue:First:_1:Valid and Clue:Second:_3:Valid
d = ClueCrate and Clue:First:_1:Valid and Clue:Second:_4:Valid
e = ClueCrate and Clue:First:_1:Valid and Clue:Second:_5:Valid
f = ClueCrate and Clue:First:_2:Valid and Clue:Second:_2:Valid
g = ClueCrate and Clue:First:_2:Valid and Clue:Second:_3:Valid
h = ClueCrate and Clue:First:_2:Valid and Clue:Second:_4:Valid
i = ClueCrate and Clue:First:_3:Valid and Clue:Second:_3:Valid

j = ClueCrate and Clue:First:_1:Valid and Clue:Second:_1:Valid and Clue:Third:_1:Valid
k = ClueCrate and Clue:First:_1:Valid and Clue:Second:_1:Valid and Clue:Third:_2:Valid
l = ClueCrate and Clue:First:_1:Valid and Clue:Second:_1:Valid and Clue:Third:_3:Valid
m = ClueCrate and Clue:First:_1:Valid and Clue:Second:_2:Valid and Clue:Third:_2:Valid

n = ClueCrate and Clue:First:_1:Valid and Clue:Second:_1:Valid and Clue:Third:_1:Valid and Clue:Fourth:_1:Valid

Crate = BlankCrate or ClueCrate

Pipe = Pipe:directions

TapaBlank = Crate or Wall or Pipe

2x2 = 2x2TopLeft or 2x2TopRight or 2x2BottomLeft or 2x2BottomRight

_TapaValue = _Black or _White
_TapaValue1 = _TapaValue
_TapaValue2 = _TapaValue
_TapaValue3 = _TapaValue
_TapaValue4 = _TapaValue
_TapaValue5 = _TapaValue
_TapaValue6 = _TapaValue

Invalidator = Clue:Position:Value:Invalid or UncluedBlock:Neighbor or ClueInPipe:Value:directions:Frame or 2x2 or Unreachable

=======
SOUNDS
=======

EndLevel 49474303

================
COLLISIONLAYERS
================

Background
CorrectBackground
2x2TopLeft
2x2TopRight
2x2BottomLeft
2x2BottomRight
Neighbor -> UncluedBlock:Neighbor
Unreachable
Player, Wall, Crate
Pipe
Position -> Clue:Position:Value:Validity
SingleClue:Value:Validity

ClueInPipe:Value:directions:Frame

_Temp
_Above, _Below, _SplitterTarget:Position
_ProcessingClues, _ProcessedClues
_Processing:Neighbor
_2x2Candidate
_StartList, _EndList
_TapaValue
_StartBlock, _StartCluedBlock
_EndBlock
_Source:Neighbor
Position -> _UnsortedClue:Position:Value
_SortCursor:Position

======
RULES     
======

( Normalise clues so they're all represented as multiclues:
  - Remove 0 clues
  - Represent single clues as multiclues with a single entry
)
[ SingleClue:_0:Validity ] -> [ ]
Value Validity [ SingleClue:Value:Validity ] -> [ Clue:First:Value:Validity ]
Position Value [ Clue:Position:Value:Invalid ] -> [ Clue:Position:Value:Valid ]

( Animate pipes )
Value [ ClueInPipe:Value:>:_0 ] -> [ > ClueCrate Clue:First:Value:Valid ]
Value Frame [ ClueInPipe:Value:>:Frame no _Temp ] -> [ ClueInPipe:Value:>:NextFrame _Temp ] again
[ _Temp ] -> [ ]

( Press X on a valid solution to complete the level )
[ action Player CorrectBackground ] -> [ Player CorrectBackground ] win

( Player can push multiple crates )
[ > Player | Crate ] -> [ > Player | > Crate ]
[ > Crate | Crate ] -> [ > Crate | > Crate ]

( Walls and pipes block movement. )
[ > Crate | Wall ] -> [ Crate | Wall ]
[ > Crate | Pipe ] -> [ Crate | Pipe ]

( Pushing two crates into a wall attempts to combine the clues. )

( Check if the clues from both crates can fit on a single crate, otherwise propagate blocking. )
[ > ClueCrate | stationary BlankCrate ] -> [ ClueCrate | BlankCrate ]
+ [ > BlankCrate | stationary Crate ] -> [ BlankCrate | Crate ]
+ [ > ClueCrate Clue:Fourth:Value:Valid | stationary ClueCrate Clue:First:Value:Valid ] -> [ ClueCrate Clue:Fourth:Value:Valid | ClueCrate Clue:First:Value:Valid ]
+ [ > ClueCrate Clue:Third:Value:Valid | stationary ClueCrate Clue:Second:Value:Valid ] -> [ ClueCrate Clue:Third:Value:Valid | ClueCrate Clue:Second:Value:Valid ]
+ [ > ClueCrate Clue:Second:Value:Valid | stationary ClueCrate Clue:Third:Value:Valid ] -> [ ClueCrate Clue:Second:Value:Valid | ClueCrate Clue:Third:Value:Valid ]
+ [ > ClueCrate Clue:First:Value:Valid | stationary ClueCrate Clue:Fourth:Value:Valid ] -> [ ClueCrate Clue:First:Value:Valid | ClueCrate Clue:Fourth:Value:Valid ]

( Otherwise, merge the two crates. )
Value1 Position1 [ > ClueCrate Clue:Position:Value1:Valid | stationary ClueCrate no Clue:Position1:Value:Valid ] -> [ > ClueCrate | ClueCrate Clue:Position1:Value1:Valid ]

[ > ClueCrate no Clue:Position:Value:Valid | stationary ClueCrate ] -> [ | ClueCrate ]

( Propagate crate movement to clues. )
[ > Crate Clue:Position:Value:Valid ] -> [ > Crate > Clue:Position:Value:Valid ]

( If the player is blocked but would push into a pipe, move the
  first clue of the last crate into the pipe. )
[ > Player | stationary Crate ] -> [ Player | Crate > _Temp ]
[ > _Temp | Crate ] -> [ | Crate > _Temp ]
Value [ Clue:First:Value:Valid > _Temp | Pipe:> ] -> [ | Pipe:> ClueInPipe:Value:>:_8 ] again
[ ClueCrate no Clue:Position:Value:Valid ] -> [ ]
[ _Temp ] -> [ ]

(------------- late rules -------------)

( Collapse remaining clues into the lowest positions )
late Value1 [ Clue:Second:Value1:Valid no Clue:First:Value:Valid ] -> [ Clue:First:Value1:Valid ]
+ late Value1 [ Clue:Third:Value1:Valid no Clue:Second:Value:Valid ] -> [ Clue:Second:Value1:Valid ]
+ late Value1 [ Clue:Fourth:Value1:Valid no Clue:Third:Value:Valid ] -> [ Clue:Fourth:Value1:Valid ]

( Sort clues via Selection Sort)
late Value Position [ Clue:Position:Value:Valid ] -> [ Clue:Position:Value:Valid _UnsortedClue:Position:Value _SortCursor:First ]

late Position [ _SortCursor:Position _Temp ] -> [ _SortCursor:NextPosition ]
+ late [ _SortCursor:Position no _UnsortedClue:Position:Value ] -> [ ]
+ late Position [ _SortCursor:Position no Clue:Position:Value:Valid ] -> [ _SortCursor:NextPosition ]
( Expand by value first so lower values get processed earlier )
+ late Value Position [ _UnsortedClue:Position1:Value _SortCursor:Position no _Temp ] -> [ Clue:Position:Value:Valid _SortCursor:Position _Temp ]

((( VALIDATION )))

( Mark unreachable cells are invalid )
late [ no TapaBlank no Player ] -> [ Unreachable ]
late [ no TapaBlank no Unreachable | Unreachable ] -> [ | ]

( Mark 2x2 areas as invalid )
late [ 2x2 ] -> [ ]
late right [ no TapaBlank | no TapaBlank ] -> [ _2x2Candidate | ]
late down [ _2x2Candidate | _2x2Candidate ] -> [ 2x2TopLeft _2x2Candidate | _2x2Candidate ]
late [ _2x2Candidate ] -> [ ]
late right [ 2x2TopLeft | ] -> [ 2x2TopLeft | 2x2TopRight ]
late down [ 2x2TopLeft | ] -> [ 2x2TopLeft | 2x2BottomLeft ]
late down [ 2x2TopRight | ] -> [ 2x2TopRight | 2x2BottomRight ]

( Validate Tapa clues )
late [ UncluedBlock:Neighbor ] -> [ ]
startloop
	( Pick a random clue crate to process and set up list markers )
	late random [ ClueCrate no _ProcessedClues ] -> [ ClueCrate _ProcessingClues _Temp ]
    late right [ _Temp ] [ | | | | | | | ] -> [ ] [ _StartList _Black _Source:_TL | _Black  _Source:_T | _Black  _Source:_TR | _Black _Source:_R | _Black _Source:_BR | _Black _Source:_B | _Black _Source:_BL | _Black _Source:_L _EndList ]
    
    ( Mark neighbors )
    late down [ | _ProcessingClues | ] -> [ _Above | _ProcessingClues | _Below ]
    late right [ | _Above | ] -> [ _Processing:_TL |  _Processing:_T | _Processing:_TR ]
    late right [ | _ProcessingClues | ] -> [ _Processing:_L |  _ProcessingClues | _Processing:_R ]
    late right [ | _Below | ] -> [ _Processing:_BL |  _Processing:_B | _Processing:_BR ]
    
    ( Record neighbors into list )
    late Neighbor [ _Source:Neighbor _Black ] [ _Processing:Neighbor TapaBlank ] -> [ _Source:Neighbor _White ] [ _Processing:Neighbor TapaBlank ]
    
    ( Rotate neighbours until start and end and aren't both black )
    late right [ _StartList _Black _Source:Neighbor | ... | _Black _EndList | no _TapaValue ] [ _White ] -> [ _StartList | ... | _Black _EndList | _Black _Source:Neighbor ] [ _White ]
    + late right [ no _TapaValue | _TapaValue _Source:Neighbor ] -> [ _TapaValue _Source:Neighbor | ]
	
    ( Mark the start and end of each block of black )
    late [ _StartList _Black ] -> [ _StartList _StartBlock _Black ]
    late [ _Black _EndList ] -> [ _Black _EndBlock _EndList ]
    late right [ _White | _Black ] -> [ _White | _StartBlock _Black ]
    late right [ _Black | _White ] -> [ _Black _Endblock | _White ]
    
    ( Mark all clues and neighbors as invalid )
    late Position Value [ _ProcessingClues Clue:Position:Value:Valid ] -> [ _ProcessingClues Clue:Position:Value:Invalid ]
    late Neighbor [ _Processing:Neighbor no TapaBlank ] -> [ _Processing:Neighbor UncluedBlock:Neighbor ]
    
    ( Clear valid clues )
    late right Position [ _StartBlock _EndBlock ] [ _ProcessingClues Clue:Position:_1:Invalid ] -> [ _StartCluedBlock _EndBlock ] [ _ProcessingClues Clue:Position:_1:Valid ]
	late right Position [ _StartBlock | _EndBlock ] [ _ProcessingClues Clue:Position:_2:Invalid ] -> [ _StartCluedBlock | _EndBlock ] [ _ProcessingClues Clue:Position:_2:Valid ]
	late right Position [ _StartBlock | _Black |  _EndBlock ] [ _ProcessingClues Clue:Position:_3:Invalid ] -> [ _StartCluedBlock | | _EndBlock ] [ _ProcessingClues Clue:Position:_3:Valid ]
	late right Position [ _StartBlock | _Black | _Black |  _EndBlock ] [ _ProcessingClues Clue:Position:_4:Invalid ] -> [ _StartCluedBlock | | | _EndBlock ] [ _ProcessingClues Clue:Position:_4:Valid ]
	late right Position [ _StartBlock | _Black | _Black | _Black |  _EndBlock ] [ _ProcessingClues Clue:Position:_5:Invalid ] -> [ _StartCluedBlock | | | | _EndBlock ] [ _ProcessingClues Clue:Position:_5:Valid ]
	late right Position [ _StartBlock | _Black | _Black | _Black | _Black |  _EndBlock ] [ _ProcessingClues Clue:Position:_6:Invalid ] -> [ _StartCluedBlock | | | | | _EndBlock ] [ _ProcessingClues Clue:Position:_6:Valid ]
	late right Position [ _StartBlock | _Black | _Black | _Black | _Black | _Black |  _EndBlock ] [ _ProcessingClues Clue:Position:_7:Invalid ] -> [ _StartCluedBlock | | | | | | _EndBlock ] [ _ProcessingClues Clue:Position:_7:Valid ]
	late right Position [ _StartBlock | _Black | _Black | _Black | _Black | _Black | _Black |  _EndBlock ] [ _ProcessingClues Clue:Position:_8:Invalid ] -> [ _StartCluedBlock | | | | | | | _EndBlock ] [ _ProcessingClues Clue:Position:_8:Valid ]

	( Clear valid neighbors )
    late right Neighbor [ _StartCluedBlock _Source:Neighbor no _EndBlock | ] [ _Processing:Neighbor UncluedBlock:Neighbor ] -> [ | _StartCluedBlock ] [ _Processing:Neighbor ]
    late right Neighbor [ _StartCluedBlock _Source:Neighbor _EndBlock ] [ _Processing:Neighbor UncluedBlock:Neighbor ] -> [ ] [ _Processing:Neighbor ]

	( If there are any invalid clues or neighbors, mark all clues as invalid )
    late Position1 Value1 [ _ProcessingClues Clue:Position1:Value1:Valid Clue:Position:Value:Invalid ] -> [ _ProcessingClues Clue:Position1:Value1:Invalid Clue:Position:Value:Invalid ]
    late Position1 Value1 Neighbor [ _ProcessingClues Clue:Position1:Value1:Valid ] [ _Processing:Neighbor UncluedBlock:Neighbor ] -> [ _ProcessingClues Clue:Position1:Value1:Invalid ] [ _Processing:Neighbor UncluedBlock:Neighbor ]
    
    ( If there are any unclued neighbors, also mark empty clue crates as invalid )
    late Neighbor [ _ProcessingClues no Clue:Position:Value:Validity ] [ _Processing:Neighbor UncluedBlock:Neighbor ] -> [ _ProcessingClues Clue:First:_0:Invalid ] [ _Processing:Neighbor UncluedBlock:Neighbor ]

	( Clean up )
    late [ _StartList ] -> [ ]
    late [ _EndList ] -> [ ]
    late [ _StartBlock ] -> [ ]
    late [ _EndBlock ] -> [ ]
    late [ _TapaValue ] -> [ ]
    late [ _Above ] -> [ ]
    late [ _Below ] -> [ ]
    late [ _Source:Neighbor ] -> [ ]
    late [ _ProcessingClues ] -> [ _ProcessedClues ]
    late [ _Processing:Neighbor ] -> [ ]
    late [ UncluedBlock:Neighbor ] -> [ ]
endloop
late [ _ProcessedClues ] -> [ ]

( Don't actually check 2x2 and connectivity rules )
late [ 2x2 ] -> [ ]
late [ Unreachable ] -> [ ]

( Mark floor as valid if no rules were violated )
late [ Background ] -> [ CorrectBackground ]
late [ CorrectBackground ] [ Invalidator ] -> [ Background ] [ Invalidator ]

( Undo clue normalisation:
  - Display empty clues as zero-clues.
  - Display single clues with special full-size sprites.
)
late [ ClueCrate no Clue:First:Value:Validity ] -> [ ClueCrate SingleClue:_0:Valid ]
late Value1 Validity1 [ Clue:First:Value1:Validity1 no Clue:Second:Value:Validity ] -> [ SingleClue:Value1:Validity1 ]

==============
WINCONDITIONS
==============

=======     
LEVELS
=======

message Press X when there is no more red in the level.

############
#.1#....####
####.84.####
#.##.25.#..#
#.2#p...#3.#
############

##############
##...#########
##.#.#########
#......#######
#.#...########
#........824p#
##..#.##.#####
#####....#####
##############

#########
##...p###
##.7..###
###.i..##
###..e.##
####...##
#########

#########
######.##
#####.p.#
######.##
#.1111.##
#......##
#########

#########
##1*2*###
##*..2###
##1..*2*#
##**p..2#
##3..1.*#
##*..4*1#
##1*2*###
#########

###############
#.............#
#.p.012345678.#
#.............#
#.*.abcdefghi.#
#.............#
#...jklm...n..#
#.............#
#.............#
#.............#
###############

#########
#.......#
#.*****.#
#.*...*.#
#p*.0.*.#
#.*...*.#
#.*****.#
#.......#
#########

############
#p.........#
#.12345678.#
#.........##
#..........#
#...═....║.#
#..........#
#..........#
############

########
#......#
#.2.═..#
#...#║.#
#####p.#
########

(

#########
##1*2*###
##*..2###
##1..*2*#
##**p..2#
##3..1.*#
##*..4*1#
##1*2*###
#########

###############
#.............#
#.p.12345678..#
#.............#
#.*.abcdefghi.#
#.............#
#...jklm...n..#
#.............#
#....+.X.-....#
#.............#
###############

###############
#..........####
#.p........####
#..1.2.3.4.####
#.............#
#.........+...#
#.........+...#
#..x..---.....#
#.............#
#.............#
###############

#########
#.......#
#.....*.#
#.P.8...#
#.......#
#.......#
#########
)

